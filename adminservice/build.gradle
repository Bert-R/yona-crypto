description = 'Administration service'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:$project.ext.springBootVersion")
		classpath("io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE")
		classpath('com.bmuschko:gradle-docker-plugin:3.0.3')
		classpath 'net.researchgate:gradle-release:2.3.4'
	}
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: "groovy"
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'net.researchgate.release'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

jar {
	baseName = 'adminservice'
}

release {
	failOnCommitNeeded = true
	tagTemplate = 'adminservice-${version}'
	git {
		requireBranch = 'yd-40-changes-for-building|master'
	}
}

configurations {
	providedRuntime
}

dependencies {
	compile project(":core")
	compile("org.springframework.boot:spring-boot-starter-data-jpa")
	compile("org.springframework.boot:spring-boot-starter-hateoas")
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("com.google.guava:guava:18.0")
	compile("org.springframework.batch:spring-batch-admin-manager:1.3.1.RELEASE") {
		exclude(module: "slf4j-log4j12")
		exclude(module: "slf4j-api")
	}
	compile("org.springframework:spring-jdbc:4.0.6.RELEASE")
	compile("org.springframework.batch:spring-batch-core:3.0.7.RELEASE")

	runtime("org.hsqldb:hsqldb")
	providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

	testCompile project(path: ':core', configuration: 'testUtils')
	testCompile("org.springframework.boot:spring-boot-starter-test")
	testCompile("junit:junit:4.12")

	// mandatory dependencies for using Spock
	testCompile "org.codehaus.groovy:groovy-all:2.4.6"
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4"

	// optional dependencies for using Spock
	testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
	testRuntime "cglib:cglib-nodep:3.1"			 // allows mocking of classes (in addition to interfaces)
	testRuntime "org.objenesis:objenesis:2.2"	 // allows mocking of classes without default constructor (together with CGLIB)
	testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
}

test {
	enableAssertions=true
}

bootRun {
	enableAssertions=true

	if ( project.hasProperty('adminservice.debug') || project.hasProperty('debug.all')) {
		jvmArgs "-Xdebug", "-Xrunjdwp:transport=dt_socket,address=" + project.ext.yona_adminservice_debug_port + ",server=y,suspend=n"
	}

	systemProperties = [
		'server.port': System.getProperty('server.port', project.ext.yona_adminservice_port)
	]
}

group = 'yonadev'

docker {
	registryCredentials {
		username = project.hasProperty('dockerHubUserName')? project.dockerHubUserName : null
		password = project.hasProperty('dockerHubPassword')? project.dockerHubPassword : null
	}
}

task buildDockerImage(type: DockerBuildImage, dependsOn: bootRepackage) {
    inputDir = mkdir("build/docker")
	tag = "${project.group}/${jar.baseName}"
	doFirst {
		copy {
			from bootRepackage
			into inputDir
			rename "${jar.baseName}-${jar.version}-${bootRepackage.classifier}", "${jar.baseName}"
		}
		copy {
			from 'src/main/docker/Dockerfile'
			into "$inputDir"
		}
	}
}

task pushDockerImage(type: DockerPushImage, dependsOn: buildDockerImage) {
	imageName = "${project.group}/${jar.baseName}"
}

sourceSets {
	intTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

configurations {
	intTestCompile.extendsFrom testCompile
	intTestRuntime.extendsFrom testRuntime
}

task intTest(type:Test){
	description = "Run integration tests (located in src/intTest/...)."
	testClassesDir = project.sourceSets.intTest.output.classesDir
	classpath = project.sourceSets.intTest.runtimeClasspath

	systemProperties = System.getProperties()

	systemProperties = [
		'yona.adminservice.url': project.ext.yona_adminservice_url,
		'yona.analysisservice.url': project.ext.yona_analysisservice_url,
		'yona.appservice.url': project.ext.yona_appservice_url
	]
}

bootRepackage {
        classifier = "full"
}
