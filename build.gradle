buildscript {
    ext {
        springBootVersion = '1.2.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
        classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: "groovy"
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot' 
apply plugin: 'io.spring.dependency-management' 
apply plugin: 'war'


war {
    baseName = 'yona-server'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    providedRuntime
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile("org.springframework.boot:spring-boot-starter-web")
	compile("io.springfox:springfox-swagger2:2.2.2")
	compile("io.springfox:springfox-swagger-ui:2.2.2")

    runtime("org.hsqldb:hsqldb") 
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    testCompile("org.springframework.boot:spring-boot-starter-test") 
    testCompile("junit:junit:4.12") 

	// mandatory dependencies for using Spock
	testCompile "org.codehaus.groovy:groovy-all:2.4.5"
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4"

	// optional dependencies for using Spock
	testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
	testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
	testRuntime "org.objenesis:objenesis:2.2"    // allows mocking of classes without default constructor (together with CGLIB)
	testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

test {
	// Enable assertions
	jvmArgs '-ea'
}

run {
	// Enable assertions
	jvmArgs '-ea'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
}

task intTest(type:Test){
    description = "Run integration tests (located in src/intTest/...)."
    testClassesDir = project.sourceSets.intTest.output.classesDir
    classpath = project.sourceSets.intTest.runtimeClasspath
}

task setupIntTest(type: Test) {
    include '**/InitialGoalCreationTest.*'
    description = "Run tintegration test setup."
    testClassesDir = project.sourceSets.intTest.output.classesDir
    classpath = project.sourceSets.intTest.runtimeClasspath
}

intTest {
    exclude '**/InitialGoalCreationTest.*'
}

intTest.dependsOn setupIntTest
